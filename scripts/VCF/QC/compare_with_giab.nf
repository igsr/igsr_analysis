#!/usr/bin/env nextflow

/* 
 * VCF benchmarking using GIAB (Genome in a Bottle)
 * This workflow relies on Nextflow (see https://www.nextflow.io/tags/workflow.html)
 *
 * @author
 * Ernesto Lowy <ernesto.lowy@gmail.com>
 *
 */

// params defaults
params.help = false

//print usage
if (params.help) {
    log.info ''
    log.info 'Pipeline to benchmark a VCF using GIAB'
    log.info '--------------------------------------'
    log.info ''
    log.info 'Usage: '
    log.info '    nextflow benchmark_giab.nf --pwd $DB_PASS --output_dir ./out --dbname elowy_hgdp_03102018 --runs runs.txt'
    log.info ''
    log.info 'Options:'
    log.info '	--help	Show this message and exit.'
    log.info '	--vcf VCF    Path to the VCF file that will be assessed.'
    log.info '  --chros CHROSTR	  Chromosomes that will be analyzed: chr20 or chr1,chr2.'
    log.info '  --prefix STR	  String used as prefix for the output VCF file.'
    log.info ''
    exit 1
}

process dropGTPs {
	/*
	This process will drop the genotypes from the initial VCF and will also select the variants and the desired chromosomes.
	Additionally, only the variants with the 'PASS' label in the filter column are considered

	Returns
	-------
	Path to a VCF file containing just the filtered sites
	*/

	output:
        file 'out.sites.vcf.gz' into out_sites_vcf

	"""
	${params.bcftools_folder}/bcftools view -c1 -G ${params.vcf} -f.,PASS -r ${params.chros} -o out.sites.vcf.gz -Oz
	${params.tabix} out.sites.vcf.gz
	"""
}


process excludeNonValid {
	/*
	This process will exclude the regions defined in a .BED file file from out_sites_vcf
	*/

	input:
	file out_sites_vcf

	output:
	file 'out.sites.nonvalid.vcf.gz' into out_sites_nonvalid_vcf

	"""
	${params.bcftools_folder}/bcftools view -T ^${params.non_valid_regions} ${out_sites_vcf} -o out.sites.nonvalid.vcf.gz -Oz
	tabix out.sites.nonvalid.vcf.gz
	"""
}


process selectSNPs {
	/*
	Process to select the SNPs from out_sites_nonvalid_vcf
	*/

	input:
	file out_sites_nonvalid_vcf

	output:
	file 'out.sites.nonvalid.snps.vcf.gz' into out_sites_nonvalid_snps_vcf

	"""
	${params.bcftools_folder}/bcftools view -v snps ${out_sites_nonvalid_vcf} -o out.sites.nonvalid.snps.vcf.gz -O z
	"""
}

process intersecionCallSets {
	/*
	Process to find the intersection between out_sites_nonvalid_snps_vcf and GIAB call set
	*/

	input:
	file out_sites_nonvalid_snps_vcf

	output:
	file 'dir/' into out_intersect

	"""
	tabix ${out_sites_nonvalid_snps_vcf}
	${params.bcftools_folder}/bcftools isec -p 'dir/' ${out_sites_nonvalid_snps_vcf} ${params.giab}
	"""
}

process compressIntersected {
	/*
	Process to compress the files generated by bcftools isec
	and to run BCFTools stats on these files
	*/
	publishDir 'results', saveAs:{ filename -> "$filename" }

	input:
	file out_intersect

	output:
	file 'FP.vcf.gz' into fp_vcf
	file 'FN.vcf.gz' into fn_vcf
	file 'TP.vcf.gz' into tp_vcf
	file 'FP.stats' into fp_stats
	file 'FN.stats' into fn_stats
	file 'TP.stats' into tp_stats

	"""
	${params.bgzip} -c ${out_intersect}/0000.vcf > FP.vcf.gz
	${params.bcftools_folder}/bcftools stats ${out_intersect}/0000.vcf > FP.stats 
	${params.bgzip} -c ${out_intersect}/0001.vcf > FN.vcf.gz
	${params.bcftools_folder}/bcftools stats ${out_intersect}/0001.vcf > FN.stats
	${params.bgzip} -c ${out_intersect}/0002.vcf > TP.vcf.gz
	${params.bcftools_folder}/bcftools stats ${out_intersect}/0002.vcf > TP.stats
	"""
}

process selectInHighConf {
	/*
	Process to select the variants in the intersected call sets
	that are in high confidence regions as defined in GIAB
	*/
	publishDir 'results', saveAs:{ filename -> "$filename" }
	

	input:
	file fp_vcf
	file fn_vcf
	file tp_vcf

	output:
	file 'FP.highconf.vcf.gz' into fp_highconf_vcf
	file 'FN.highconf.vcf.gz' into fn_highconf_vcf
	file 'TP.highconf.vcf.gz' into tp_highconf_vcf
	file 'FP.highconf.stats' into fp_highconf_stats
	file 'FN.highconf.stats' into fn_highconf_stats
	file 'TP.highconf.stats' into tp_highconf_stats
	"""
	tabix ${fp_vcf}
	tabix ${fn_vcf}
	tabix ${tp_vcf}
	${params.bcftools_folder}/bcftools view -R ${params.high_conf_regions} ${fp_vcf} -o FP.highconf.vcf.gz -Oz
	${params.bcftools_folder}/bcftools stats FP.highconf.vcf.gz > FP.highconf.stats
	${params.bcftools_folder}/bcftools view -R ${params.high_conf_regions} ${fn_vcf} -o FN.highconf.vcf.gz -Oz
	${params.bcftools_folder}/bcftools stats FN.highconf.vcf.gz > FN.highconf.stats
	${params.bcftools_folder}/bcftools view -R ${params.high_conf_regions} ${tp_vcf} -o TP.highconf.vcf.gz -Oz
	${params.bcftools_folder}/bcftools stats TP.highconf.vcf.gz > TP.highconf.stats
	"""
}
