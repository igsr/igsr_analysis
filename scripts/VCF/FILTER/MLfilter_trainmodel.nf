/* 
 * Workflow to filter a VCF
 * This workflow relies on Nextflow (see https://www.nextflow.io/tags/workflow.html)
 *
 * @author
 * Ernesto Lowy <ernesto.lowy@gmail.com>
 *
 */

// params defaults
params.help = false
params.threads = 1
params.queue = 'production-rh7'

//print usage
if (params.help) {
    log.info ''
    log.info 'Pipeline to train a Logistic Regression binary classifier'
    log.info '---------------------------------------------------------'
    log.info ''
    log.info 'Usage: '
    log.info '    nextflow MLfilter_trainmodel.nf --vcf VCF --true VCF --vt snps --annotations ANNOTATION_STRING --threads 5'
    log.info ''
    log.info 'Options:'
    log.info '	--help	Show this message and exit.'
    log.info '	--vcf VCF    Path to the VCF file that will be used for training.'
    log.info '  --true VCF  Path to the VCF file containing the gold-standard sites.'
    log.info '  --vt  VARIANT_TYPE   Type of variant to use for training the model. Poss1ible values are 'snps'/'indels'.'
    log.info '  --annotations ANNOTATION_STRING	String containing the annotations to filter, for example:'
    log.info '	%CHROM\t%POS\t%INFO/DP\t%INFO/RPB\t%INFO/MQB\t%INFO/BQB\t%INFO/MQSB\t%INFO/SGB\t%INFO/MQ0F\t%INFO/ICB\t%INFO/HOB\t%INFO/MQ\n.' 
    log.info '  --threads INT Number of threads used in the different BCFTools processes. Default=1.'
    log.info ''
    exit 1
}


//Training the classifier. For all sites (across all the chros)

process excludeNonVariants {
	/*
	This process will select the variants on the unfiltered vcf (all chros) for
	the particular type defined by 'params.vt'

	Returns
	-------
	Path to a site-VCF file containing just the variants on a particular chromosome
	*/

	memory '500 MB'
        executor 'local'
        queue "${params.queue}"
        cpus "${params.threads}"

	output:
	file "out.sites.${params.vt}.vcf.gz" into out_sites_vts

	"""
	bcftools view -v ${params.vt} -G -c1 ${params.vcf} -o out.sites.${params.vt}.vcf.gz --threads ${params.threads} -Oz
	tabix out.sites.${params.vt}.vcf.gz
	"""
}

process intersecionCallSets {
	/*
	Process to find the intersection between out_sites_vts and the Gold standard call set
	*/

	memory '500 MB'
        executor 'local'
        queue "${params.queue}"
        cpus 1

	input:
	file out_sites_vts

	output:
	file 'dir/' into out_intersect

	"""
	tabix ${out_sites_vts}
	bcftools isec -c ${params.vt}  -p 'dir/' ${out_sites_vts} ${params.true}
	"""
}

process compressIntersected {
        /*
        Process to compress the files generated by bcftools isec
        */
        memory '500 MB'
        executor 'local'
        queue "${params.queue}"
        cpus 1

        input:
        file out_intersect

        output:
        file 'FP.vcf.gz' into fp_vcf
        file 'FN.vcf.gz' into fn_vcf
        file 'TP.vcf.gz' into tp_vcf

        """
        bgzip -c ${out_intersect}/0000.vcf > FP.vcf.gz
        bgzip -c ${out_intersect}/0001.vcf > FN.vcf.gz
        bgzip -c ${out_intersect}/0002.vcf > TP.vcf.gz
        """
}

process get_variant_annotations {
	/*
	Process to get the variant annotations for training files 
	and for VCF file to annotate (for a single chromosome in this case)
	*/

	memory '2 GB'
        executor 'local'
        queue "${params.queue}"
        cpus 1

	input:
	file tp_vcf
	file fp_vcf

	output:
	file 'TP_annotations.tsv.gz' into tp_annotations
	file 'FP_annotations.tsv.gz' into fp_annotations

	"""
	bcftools query -H -f '${params.annotations}' ${tp_vcf} | bgzip -c > TP_annotations.tsv.gz
	bcftools query -H -f '${params.annotations}' ${fp_vcf} | bgzip -c > FP_annotations.tsv.gz
	"""
}

process train_model {
	/*
	Process that takes TP_annotations.tsv and FP_annotations.tsv created above and will train the Logistic
	Regression binary classifier
	*/

	memory '5 GB'
        executor 'local'
        queue "${params.queue}"
        cpus 1

	publishDir "trained_model", mode: 'copy', overwrite: true

	input:
	file tp_annotations
	file fp_annotations

	output:
	file 'fitted_logreg_snps.sav' into trained_model

	"""
	#!/usr/bin/env python

	from VCF.VCFfilter.MLclassifier import MLclassifier

	ML_obj=MLclassifier()

	outfile=ML_obj.train(outprefix="fitted_logreg_snps",
			tp_annotations='${tp_annotations}',
			fp_annotations='${fp_annotations}')
	"""
}
